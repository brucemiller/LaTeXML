# -*- mode: Perl -*-
# /=====================================================================\ #
# | TeX_Kern                                                            | #
# | Core TeX Implementation for LaTeXML                                 | #
# |=====================================================================| #
# | Part of LaTeXML:                                                    | #
# |  Public domain software, produced as part of work done by the       | #
# |  United States Government & not subject to copyright in the US.     | #
# |---------------------------------------------------------------------| #
# | Bruce Miller <bruce.miller@nist.gov>                        #_#     | #
# | http://dlmf.nist.gov/LaTeXML/                              (o o)    | #
# \=========================================================ooo==U==ooo=/ #
package LaTeXML::Package::Pool;
use strict;
use warnings;
use LaTeXML::Package;

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
# Kern Family of primitive control sequences
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

#======================================================================
# Basic kerning
#----------------------------------------------------------------------
# \kern             c  adds a kern item to the current list.
# \unkern           c  removes a kern from the current list.
# \lastkern         iq is 0.0 pt or the last kern on the current list.

# \kern is heavily used by xy.
# Note that \kern should add vertical spacing in vertical modes!
DefConstructor('\kern Dimension', sub {
    my ($document, $length, %props) = @_;
    my $parent = $document->getNode;
    if ($document->getNodeQName($parent) eq 'svg:g') {
      if (my $x = $length->pxValue) {
        my $transform = $parent->getAttribute('transform');
        $parent->setAttribute(transform => ($transform ? $transform . ' ' : '') . "translate($x,0)");
        if (my @nodes = $parent->childNodes) {
          # nodes have already been deposited, wrap those in svg:g to avoid shifting them
          # the code below is essentially Document::wrapNodes, but we add the transform attribute
          # *before* running afterClose, or collapseSVGGroup will immediately remove the wrapper

          # Check if any of @nodes, or any of it's children, are the current node, and thus still "open"
          my $leave_open = 0;
          foreach my $n (@nodes) {
            if ($document->isOpen($n)) {
              $leave_open = 1;
              last; } }

          my $model = $document->getModel;
          my ($ns, $tag) = $model->decodeQName('svg:g');
          my $new = $document->openElement_internal($parent, $ns, $tag);
          $new->setAttribute(transform => 'translate(' . -$x . ',0)');
          $document->afterOpen($new);
          $parent->replaceChild($new, $nodes[0]);

          if (my $font = $document->getNodeFont($parent)) {
            $document->setNodeFont($new, $font); }
          if (my $box = $document->getNodeBox($parent)) {
            $document->setNodeBox($new, $box); }
          foreach my $node (@nodes) {
            $new->appendChild($node); }
          $document->afterClose($new) unless $leave_open; }
    } }
    elsif (inSVG()) {
      Warn('unexpected', 'kern', $_[0], "Lost kern in SVG " . ToString($length)); }
    elsif ($props{isMath}) {
      $document->insertElement('ltx:XMHint', undef, width => $length); }
    else {
      # Add space to document?
      $document->absorb(DimensionToSpaces($length)); }
  },
  properties => sub {
    my ($stomach, $length) = @_;
    (width => $length, isSpace => 1, isKern => 1); });

# Remove kern, if last on LIST
DefPrimitiveI('\unkern', undef, sub {
    my ($stomach) = @_;
    my @c = ();
    my $box;
    # Scan past any Comment boxes
    while (($box = $LaTeXML::LIST[-1]) && (ref $box eq 'LaTeXML::Core::Comment')) {
      push(@c, pop(@LaTeXML::LIST)); }
    if ($box && $box->getProperty('isKern')) {
      pop(@LaTeXML::LIST); }
    push(@LaTeXML::LIST, @c);
    return; });

# Get kern, if last on LIST
DefRegister('\lastkern' => Dimension(0), readonly => 1, getter => sub {
    for (my $i = $#LaTeXML::LIST ; $i > 0 ; $i--) {
      my $box = $LaTeXML::LIST[$i];
      last if !$box;
      next if ref $box eq 'LaTeXML::Core::Comment';
      last if !$box->getProperty('isKern');
      return $box->getProperty('width'); }
    return Dimension(0); });

#======================================================================
# Moving Vertically
#----------------------------------------------------------------------
# \raise            c  shifts a box up and appends it to the current horizontal or math list.
# \lower            c  shifts a box down and appends it to the current horizontal or math list.
# \lower <dimen> <box>
# \raise <dimen> <box>
# But <box> apparently must really explicitly be an \hbox, \vbox or \vtop (?)
# OR something that expands into one!!
sub raisedSizer {
  my ($box, $y) = @_;
  my ($w, $h, $d) = $box->getSize;
  my $z = Dimension(0);
  $h = $h->add($y)->larger($z);
  $d = $d->subtract($y)->larger($z);
  return ($w, $h, $d); }

DefConstructor('\lower Dimension MoveableBox',
  "?&inSVG()(<svg:g transform='#transform' _noautoclose='1'>#2</svg:g>)"
    . "(<ltx:text yoffset='#y'  _noautoclose='1'>#2</ltx:text>)",
  sizer       => sub { raisedSizer($_[0]->getArg(2), $_[0]->getArg(1)->negate); },
  afterDigest => sub {
    my $y         = $_[1]->getArg(1)->multiply(-1);
    my $ypx       = $y->pxValue;
    my $transform = ($ypx ? "translate(0,$ypx)" : undef);
    $_[1]->setProperties(y => $y, transform => $transform); });

DefConstructor('\raise Dimension MoveableBox',
  "?&inSVG()(<svg:g transform='#transform' _noautoclose='1'>#2</svg:g>)"
    . "(<ltx:text yoffset='#y'  _noautoclose='1'>#2</ltx:text>)",
  sizer       => sub { raisedSizer($_[0]->getArg(2), $_[0]->getArg(1)); },
  afterDigest => sub {
    my $y         = $_[1]->getArg(1);
    my $ypx       = $y->pxValue;
    my $transform = ($ypx ? "translate(0,$ypx)" : undef);
    $_[1]->setProperties(y => $y, transform => $transform); });

#======================================================================
# Moving Horizontally
#----------------------------------------------------------------------
# \moveleft         c  shifts a box left and appends it to the current vertical list.
# \moveright        c  shifts a box right and appends it to the current vertical list.
# \moveleft<dimen><box>, \moveright<dimen><box>
DefConstructor('\moveleft Dimension MoveableBox',
  "<ltx:text xoffset='#x' _noautoclose='1'>#2</ltx:text>",
  afterDigest => sub {
    $_[1]->setProperty(x => $_[1]->getArg(1)->multiply(-1)); });
DefConstructor('\moveright Dimension MoveableBox',
  "<ltx:text xoffset='#x' _noautoclose='1'>#2</ltx:text>",
  afterDigest => sub {
    $_[1]->setProperty(x => $_[1]->getArg(1)); });

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
1;
