# -*- mode: Perl -*-
# /=====================================================================\ #
# |  graphicx                                                           | #
# | Implementation for LaTeXML                                          | #
# |=====================================================================| #
# | Part of LaTeXML:                                                    | #
# |  Public domain software, produced as part of work done by the       | #
# |  United States Government & not subject to copyright in the US.     | #
# |---------------------------------------------------------------------| #
# | Bruce Miller <bruce.miller@nist.gov>                        #_#     | #
# | http://dlmf.nist.gov/LaTeXML/                              (o o)    | #
# \=========================================================ooo==U==ooo=/ #
package LaTeXML::Package::Pool;
use strict;
use warnings;
use LaTeXML::Package;
use LaTeXML::Util::Image;

#**********************************************************************
# (See  LaTeXML::Post::Graphics for suggested postprocessing)
# Provides alternative argument syntax.
RequirePackage('graphics');

DefMacro('\Gin@ewidth',   '');
DefMacro('\Gin@eheight',  '');
DefMacro('\Gin@eresize',  '');
DefMacro('\Gin@esetsize', '');
#DefKeyVal('Gin', 'width',           'Dimension', '', code=>sub { DefMacro('\Gin@ewidth',$_[1]); });
DefKeyVal('Gin', 'width',           'GraphixDimension');
DefKeyVal('Gin', 'height',          'GraphixDimension');
DefKeyVal('Gin', 'totalheight',     'GraphixDimension');
DefKeyVal('Gin', 'keepaspectratio', '', 'true');
DefKeyVal('Gin', 'clip',            '', 'true');
DefKeyVal('Gin', 'scale',           '');
DefKeyVal('Gin', 'angle',           '');
DefKeyVal('Gin', 'actualtext',      '');
DefKeyVal('Gin', 'alt',             '');
DefKeyVal('Gin', 'artifact',        '', 'true');
DefKeyVal('Gin', 'trim',            'GraphixDimensions');
DefKeyVal('Gin', 'viewport',        'GraphixDimensions');
# NOTE: graphicx defines @angle to actually carry out the rotation (on \box\z@) w/\Gin@erotate
# rather than to simply record the angle for later use. (also origin redefines)
# This is used by adjustbox.
# See \Gin@erotate, \Grot@box

# LaTeXML extensions:
DefKeyVal('Gin', 'vrml', 'Semiverbatim');
DefKeyVal('Gin', 'magnifiable', '', 'true');

# Redefine
DefMacro('\includegraphics OptionalMatch:* []',
  '\@ifnextchar[{\@includegraphics#1[#2]}{\@includegraphicx#1[#2]}', scope => 'global');

DefConstructor('\@includegraphicx OptionalMatch:* OptionalKeyVals:Gin Semiverbatim',
  "<ltx:graphics graphic='#path' candidates='#candidates' options='#options'/>",
  alias          => '\includegraphics',
  sizer          => \&image_graphicx_sizer,
  scope          => 'global',
  afterConstruct => sub {
    my ($document, $whatsit) = @_;
    my $actualtext = $whatsit->getProperty('actualtext');
    my $alt        = $whatsit->getProperty('alt');
    my $artifact   = $whatsit->getProperty('artifact');
    $artifact = 1 if defined $artifact && ToString($artifact) ne 'false';
    if ($artifact || defined $actualtext || defined $alt) {
      my $node = $document->getNode->lastChild;
      # Trickery to set @description EVEN IF empty string (constructor shorthand omits it)
      $node->setAttribute(description => ToString($actualtext // $alt // ''));
      # when both actualtext and alt are specified, use aria-description for alt
      $document->setAttribute($node, 'aria:description', ToString($alt)) if defined $alt && defined $actualtext;
      $document->setAttribute($node, 'aria:hidden', 'true')              if $artifact; } },
  properties => sub {
    my ($stomach, $starred, $kv, $graphic) = @_;
    my $options = graphicX_options($starred, $kv);
    my ($path, @candidates) = image_candidates(ToString($graphic));
    my $alt        = $kv && $kv->getValue('alt');
    my $actualtext = $kv && $kv->getValue('actualtext');
    my $artifact   = $kv && $kv->getValue('artifact');
    (path => $path,
      candidates => join(',', @candidates),
      actualtext => $actualtext,
      alt        => $alt,
      artifact   => $artifact,
      options    => $options); },
  mode => 'restricted_horizontal', enterHorizontal => 1);

#**********************************************************************
1;
